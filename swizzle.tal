( For those who want to make a change )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1
|a0 @File/vector $2 &success $1 &success-lo $1 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2/vector $2 &success $1 &success-lo $1 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

|000

	@site &name $11 &domain $21 &yz $3 &bg $7 &fg1 $7 &fg2 $7 &fg3 $7 &fg4 $7

|100

%HCF { str/<print> #010f DEO BRK }

@on-reset ( -> )
	#800f DEO ( error handling thing )
	config/<read>
	css/<write-vars>
	BRK

@config/<read>
	;path/swizzle .File/name DEO2
	#0001 .File/length DEO2 ( 1 byte at a time )
	.site/name #10 /<read-item>
	.site/domain #20 /<read-item>
	.site/yz #02 /<read-item>
	.site/bg #06 /<read-item>
	.site/fg1 #06 /<read-item>
	.site/fg2 #06 /<read-item>
	.site/fg3 #06 /<read-item>
	.site/fg4 #06 /<read-item>

	JMP2r

@config/<read-item>
	INC ADDk NIP SWP
	&>s
		( limit ptr ) GTHk
		( or else ) ?{ ;error/config HCF }

		;data/c ( stores ) .File/read DEO2
		;data/c LDA
		#0a ( must be ) NEQk
		( or else ) ?{ POP2 POP2 JMP2r }
		( and not ) POP SWP STZk INC NIP
		;&>s JMP2

@css/<write-vars>
	;path/vars .File2/name DEO2
	;data/root util/<fwrite>
	#01 .File2/append DEO
	;ascii/lcb util/<fwrite>
	.site/bg ;data/bg /<write-var>
	.site/fg1 ;data/fg1 /<write-var>
	.site/fg2 ;data/fg2 /<write-var>
	.site/fg3 ;data/fg3 /<write-var>
	.site/fg4 ;data/fg4 /<write-var>
	;ascii/rcb util/<fwrite>
	;ascii/newline util/<fwrite>
	#00 .File2/append DEO

	JMP2r

@css/<write-var> ( v* k* -- )
	;data/dash2 util/<fwrite>
	( k* ) util/<fwrite>
	;ascii/colon util/<fwrite>
	;ascii/hash util/<fwrite>
	( v* ) util/<fwrite>
	;ascii/semicolon util/<fwrite>

	JMP2r

@str/<print> ( str* -- )
	OVR ?{ #00 SWP }
	&>cp
		LDAk DUP ?{ POP POP2 #0a18 DEO JMP2r }
		#18 DEO
		INC2 !/>cp

@util/<fwrite> ( str* -- )
	OVR ?{ #00 SWP }
	DUP2
	&>ct
		LDAk ?{ OVR2 SUB2 .File2/length DEO2 .File2/write DEO2 JMP2r }
		INC2 !/>ct

@data/bg "bg $1
@data/c ( private ) $1
@data/dash2 2d 2d $1
@data/fg1 "fg1 $1
@data/fg2 "fg2 $1
@data/fg3 "fg3 $1
@data/fg4 "fg4 $1
@data/root ":root $1

@ascii/newline 0a $1 &hash 23 $1 &percent 25 $1 &dash 2d $1 &colon 3a $1 &semicolon 3b $1 &lcb 7b $1 &rcb 7d $1

@path/swizzle ".swizzle $1
	&vars "assets/vars.css $1

@error/config "Could 20 "not 20 "load 20 "config! $1
